import org.eclipse.smarthome.core.library.types.DecimalType

var Number setpoint = 0
var Number delay_count = 0

val int MIN_SETPOINT = 30
val int MAX_SETPOINT = 220
val int DELAY = 20
val float SETPOINT_POWER = 220.0 / 3000.0

rule elo_heat_control
when
    Time cron "0/5 * * * * ? *"
then
    var P_L1 = (Vic_Sys_Ac_Grid_L1_Power.state as QuantityType<Power>)
    var P_L2 = (Vic_Sys_Ac_Grid_L2_Power.state as QuantityType<Power>)
    var P_L3 = (Vic_Sys_Ac_Grid_L3_Power.state as QuantityType<Power>)

    var p_grid = P_L1 + P_L2 + P_L3
    var p_delta = -800 -p_grid.toBigDecimal 
    if (p_delta < 0){
        setpoint = setpoint + p_delta * SETPOINT_POWER
    }else{
        setpoint = setpoint + p_delta * 0.5 * SETPOINT_POWER
    }
    
    if (setpoint > MAX_SETPOINT){
        setpoint = MAX_SETPOINT
    }
    if (setpoint < 0){
        setpoint = 0
    }
    if (setpoint > MIN_SETPOINT){
        if (delay_count < DELAY) {
            delay_count = delay_count + 1
            setpoint = MIN_SETPOINT
             logInfo("elo_heat_control", "delaying.")
        }else{
            if (ELO_HEAT.state == ON){
                U_EL.sendCommand(setpoint)
                logInfo("elo_heat_control", "setting: " + setpoint.toString)
            }
        }
    }else{
        delay_count = 0
    }

    if (ELO_HEAT.state == OFF){
        U_EL.sendCommand(0)
        logInfo("elo_heat_control", "Off. Sending 0.")
    }
    
end